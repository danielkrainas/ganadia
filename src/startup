#!/bin/csh
# Written by Furey.
# With additions from Tony and Alander.

# Set the port number.
set port = 7777
if ( "$1" != "" ) set port="$1"

# Change to area directory.
cd ../area

# Set limits.
if ( -e shutdown.txt ) rm -f shutdown.txt

while ( 1 )
    # If you want to have logs in a different directory,
    #   change the 'set logfile' line to reflect the directory name.
    set index = 1000
    while ( 1 )
        set logfile = ../log/$index.log
        if ( ! -e $logfile ) break
        @ index++
    end

    # Run rom.
    ../src/swreality $port >&! $logfile


    # Restart, giving old connections a chance to die.
    if ( -e shutdown.txt ) then
        rm -f shutdown.txt
        exit 0
    endif
    sleep 15
end

# auto indent command
# indent -nbad -bap -nbc -bbo -hnl -br -brs -c33 -ncdb -ce -ci4 -cli0 -d0 -di1 -nfc1 -i8 -ip0 -l80 -lp -npcs -nprs -npsl -sai -saf -saw -ncs -nsc -sob -nfca -cp33 -ss -ts8 ./ganadia-original/src/*.c
